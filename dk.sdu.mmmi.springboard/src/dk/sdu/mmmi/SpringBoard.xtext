grammar dk.sdu.mmmi.SpringBoard with org.eclipse.xtext.common.Terminals

generate springBoard "http://www.sdu.dk/mmmi/SpringBoard"

SpringBoard:
	'package' ':' pkg=Package models=Models services=Services
		
;

Package:
	name=ID ('.' next=Package)?
;

Models:
	'models' ':' types+=Model+
;

Model:
	name=ID inh=Inherit? '{' fields+=Field* '}'
;

Field:
	name=ID ':' type=Type (inv=Invariant)?
;

Invariant:
	'[' prop=Property op=Operator INT ']'
;

Property:
	'length'
;

Operator:
	'<' | '>' | '=' | '<=' | '>=' | '<>'
;

Type:
	{Str} 'string' | {Dt} 'datetime' | {Lon} 'long' | {Int} 'int' | {Bool} 'bool' | {ModelType} base=[Model] | {ListOf} ('List' 'of' type=Type) | {Identifier} 'ID'
;

Inherit:
	'inherits' base=[Model]
;

Services:
	'services' ':' services+=Service+
;

Service:
	base=[Model] '{' crud=CRUD? methods+=Methods* '}'
;

CRUD: // See metamodel later
	'[' act += CRUDActions* ']'
;

enum CRUDActions:
	C | R | U | D
;

Methods:
	isLocal='local'? name=ID ':' type=Type inp=Input res=Res?
;

Res:
	'[' exp=Exp ']'
;

Exp:
	left=[Args] op=Operator right=[Field] (',' next=Exp)?
;

Input:
	{Input} '(' args=Args? ')'
;

Args:
	name=ID ':' type=Type (',' next=Args)?
;

