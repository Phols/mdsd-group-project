grammar dk.sdu.mmmi.SpringBoard with org.eclipse.xtext.common.Terminals

generate springBoard "http://www.sdu.dk/mmmi/SpringBoard"

SpringBoard:
	'package' ':' pkg=Package 'models' ':' models+=Model+ 'services' ':' services+=Service+ security=Security?	
;

Package:
	name=ID ('.' next=Package)?
;

Model:
	name=ID inh=Inherit? '{' fields+=Field* '}'
;

Field:
	name=ID ':' type=Type (inv=Invariant)?
;

Invariant:
	'[' prop=Property op=Operator value=INT ']'
;

Property:
	'length'
;

Operator returns Operator:
	{Lt} '<' | {Gt} '>' | {Eq} '=' | {Lteq} '<=' | {Gteq} '>=' | {Neq} '<>'
;

Type returns Type:
	{Str} 'string' | {Dt} 'datetime' | {Lon} 'long' | {Int} 'int' | {Bool} 'bool' | {Flt} 'float' | {ModelType} (base=[Model] direction='*'?) | {ListOf} ('List' 'of' type=Type) | {Identifier} 'ID'
;

Inherit:
	'inherits' base=[Model]
;

Service:
	base=[Model] '{' crud=CRUD? methods+=Method* '}'
;

CRUD:
	'[' act += CRUDActions* ']'
;

enum CRUDActions:
	C | R | U | D
;

Method:
	req=Request name=ID inp=Input ':' type=Type res=Res? apipath=API?
;

API:
	"(" STRING ")"
;

Request returns Request:
	{Local} 'local' | {Post} 'POST' | {Get} 'GET' | {Put} 'PUT' | {Delete} 'DELETE'
;

Res:
	  '{'expression=Logic'}'
;

Comp:
	left=[Args] op=Operator right=[Field]
;

Input:
	{Input} '(' args=Args? ')'
;

Args:
	name=ID ':' type=Type (',' next=Args)?
;

Security:
	{Security} 'security' ':' securities+=SecurityOption*
;

SecurityOption:
	securityConfig=SecurityConfig | 'Role' '{' roles+=Role+ '}' | 'InvariantRestrictions' '{' requestRestrictions+=RequestRestriction+ '}' | ipwhitelist=IPWhitelist
;

SecurityConfig:
	'WebSecurityConfig' '{' optionalSettings+=SecOption+ '}'
;

IPWhitelist:
	'IPRange' '{'ipAddresses=MultipleIPs'}'
;

RequestRestriction:
	request=Request ':' role=[Role]
;

Role:
	name=ID	
;

SecOption returns SecOption:
	encoder=Encoder | http=HTTP | limitedipAddress=LimitedIP | detailService=DetailService | roles=RoleRequirement
;

HTTP:
	'http' ':' type=ID
;

Encoder:
	'encoder' ':' name=ID
;

DetailService:
	base=[Model]'-service'
;

LimitedIP:
	'LimitIP' ':' base=[Method] '(' ipAddress=IP ')'
;
IP:
	INT ('.' INT)*
;

MultipleIPs:
	first=IP(',' next+=IP)*
;


RoleRequirement:
	'Authorisation' ':'  base=[Method] '('roles=AuthorisedRoles')'
;

AuthorisedRoles:
	role=[Role] (',' roles+=[Role])*
;

Logic returns Logic:
	left=Conjunction({LogicOr.left=current}'||' right=Logic)?
;

Conjunction returns Logic:
	left=PrimitiveOp ({LogicAnd.left=current}'&&' right=Conjunction)?
;

PrimitiveOp returns Logic:
 	comp=Comp |  '('Logic')'
 ;